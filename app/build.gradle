buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        //find the latest io.fabric.tools:gradle here: https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.25.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder. Copied from: https://developer.android.com/studio/publish/app-signing.html#secure-shared-keystore
def keystorePropertiesFile = rootProject.file("../MyHitchhikingSpots-private/keystore.properties")
def otherPrivatePropertiesFile = rootProject.file("../MyHitchhikingSpots-private/other-private.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
def otherPrivateProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
otherPrivateProperties.load(new FileInputStream(otherPrivatePropertiesFile))

def minSdk = hasProperty('minSdk') ? minSdk : 15

ext {
    supportLibVersion = '25.3.1'
}

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [fabricApiKey: otherPrivateProperties['fabricApiKey']]
        applicationId 'com.myhitchhikingspots'
        minSdkVersion minSdk
        targetSdkVersion 26
        versionCode 21
        versionName "2.1.5"
        // Stops the Gradle pluginâ€™s automatic rasterization of vectors
        generatedDensities = []
        // Enabling multidex support.
        multiDexEnabled true
    }

    // Flag to tell aapt to keep the attribute ids around
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    productFlavors {
    }
    sourceSets { main { res.srcDirs = ['src/main/res', 'src/main/res/values-pt'] } }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    //Begin: Dependencies for MapBox
    // add the Mapbox SDK dependency below
    implementation('com.mapbox.mapboxsdk:mapbox-android-sdk:5.0.2@aar') {
        transitive = true
    }
    //End: Dependencies for MapBox
    implementation 'cz.msebera.android:httpclient:4.4.1.1'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.4@aar') {
        transitive = true;
    }
    implementation 'org.greenrobot:greendao:3.2.0'
    implementation 'com.google.android.gms:play-services-location:11.0.4'
    implementation 'joda-time:joda-time:2.9.2'
    implementation 'com.android.support:multidex:1.0.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.github.florent37:viewtooltip:1.0.8'
    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
}
